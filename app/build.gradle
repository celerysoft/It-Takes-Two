apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId "com.celerysoft.ittakestwo"
        versionCode 4
        versionName "1.2.0"
        minSdkVersion 14
        targetSdkVersion 24
        manifestPlaceholders = [
                UMENG_APPKEY : UMENG_APPKEY_FOR_IT_TAKES_TWO,
                TENCENT_AUTH_SCHEME : "tencent" + QQ_APP_ID_FOR_IT_TAKES_TWO,
                WECHAT_APP_ID : WECHAT_APP_ID_FOR_IT_TAKES_TWO,
                WECHAT_APP_SECRET : WECHAT_APP_SECRET_FOR_IT_TAKES_TWO,
                QQ_APP_ID : QQ_APP_ID_FOR_IT_TAKES_TWO,
                QQ_APP_KEY : QQ_APP_KEY_FOR_IT_TAKES_TWO
        ]
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }

        release {
            storeFile file("celerysoft.jks")
            storePassword CELERY_STORE_PASSWORD
            keyAlias CELERY_KEY_ALIAS
            keyPassword CELERY_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " - Debug"
            signingConfig signingConfigs.debug

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('debug.apk')) {
                        def newName = "ItTakesTwo-" + "${defaultConfig.versionName}" + "-debug.apk"
                        output.outputFile = new File(outputFile.parent, newName)
                    }
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                        def newName = "ItTakesTwo-${defaultConfig.versionName}-release-${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, newName)
                    }
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    productFlavors {
        Wandoujia {
            manifestPlaceholders = [ UMENG_CHANNEL_NAME:"Wandoujia" ]
        }
        Yingyongbao {
            manifestPlaceholders = [ UMENG_CHANNEL_NAME:"Yingyongbao" ]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile files('libs/Tencent_libammsdk.jar')
    compile files('libs/Tencent_open_sdk_r5509.jar')
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.celerysoft:materialdesigndialog:1.0.2'
    compile 'com.umeng.analytics:analytics:latest.integration'
}
